Using System
Using System.Data
Using System.Configuration
Using System.Collections
Using System.Web
Using System.Web.Security
Using System.Web.UI
Using System.Web.UI.WebControls
Using System.Web.UI.WebControls.WebParts
Using System.Web.UI.HtmlControls

BegClass views_ListCustomers Partial(*Yes) Access(*Public) Extends(System.Web.UI.Page)

    DclDB DGServer DBName( "*PUBLIC/DG NET Local" )

    DclDiskFile  CustomerByName +
          Type(*Input) +
          Org(*Indexed) +
          Prefix(Customer_) +
          File("Examples/CMastNewL2") +
          DB(DGServer) +
          ImpOpen(*No)

    DclMemoryFile CustomerByNameMF2 
        DclRecordFormat Customers 
        DclRecordFld    Customer_CMCustNo  Type(*Packed) Len(9,0)
        DclRecordFld    Customer_CMName    Type(*Char) Len(40)    

    BegSr Page_Load Access(*Private) Event(*This.Load)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        Page.Title = "Work with customers"

        ConnectServer()

        If (NOT Page.IsPostBack)
            CheckForStartingPosition()
            LoadGrid()
            //
            // Called the first time that the page is loaded.
            //
        Else
            //
            // Called subsequent times that the page is displayed.
            //
        EndIf
    EndSr

    BegSr Page_Unload Access(*Private) Event(*This.Unload)
        DclSrParm sender Type(*Object)
        DclSrParm e Type(System.EventArgs)

        DisconnectServer()
    EndSr

    BegSr LoadGrid 
        DclFld Row Type(*Integer4)

        For (Row = 1) To(12)
            Read CustomerByName
            If (CustomerByName.IsEof)
                Leave
            EndIf
            Write CustomerByNameMF2
        EndFor

        gridviewCustomer.DataSource = CustomerByNameMF2.DataSet.Tables[0]
        gridviewCustomer.DataBind()
    EndSr

    BegSr ConnectServer
        Connect DGServer
        Open CustomerByName
    EndSr

    BegSr DisconnectServer
        Disconnect DGServer
        Close *All
    EndSr

	BegSr gridviewCustomer_SelectedIndexChanged Access(*Private) Event(*This.gridviewCustomer.SelectedIndexChanged)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld SelectedRow Type( *Integer4 ) 
        
        SelectedRow = gridviewCustomer.SelectedIndex        
        
        Session["customernumber"] = gridviewCustomer.DataKeys[ SelectedRow ]["customer_cmcustno"]

        // Get customer number and name from the top (zeroth) row.
        Session["firstrownumber"] = gridviewCustomer.DataKeys[0]["customer_cmcustno"]
        Session["firstrowname"  ] = gridviewCustomer.DataKeys[0]["customer_cmname"]
        
        Response.Redirect("updatecustomer.aspx")	
	EndSr

	BegSr buttonFirst_Click Access(*Private) Event(*This.buttonFirst.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        SetLL CustomerByName Key(*Start)
        LoadGrid()		
	EndSr

   	BegSr buttonNext_Click Access(*Private) Event(*This.buttonNext.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        DclFld LastRowIndex Type(*Integer4)

        // Fetch key values from last row.
        LastRowIndex = gridviewCustomer.Rows.Count - 1
        // Be careful! The DataKeys key names are case-sensitive!
        Customer_CMName = gridviewCustomer.DataKeys[LastRowIndex]["customer_cmname"].ToString()
        Customer_CMCustNo = gridviewCustomer.DataKeys[LastRowIndex]["customer_cmcustno"].ToString()
        // Position file to that row.
        SetLL CustomerByName Key(Customer_CMName, Customer_CMCustNo)
        LoadGrid()		
	EndSr

	BegSr buttonPositionTo_Click Access(*Private) Event(*This.buttonPositionTo.Click)
		DclSrParm sender Type(*Object)
		DclSrParm e Type(System.EventArgs)

        Customer_CMName = textboxPositionTo.Text
        SetLL CustomerByName Key( Customer_CMName ) 
        
        If ( CustomerByName.IsFound )
            LoadGrid()		
        Else
            textboxPositionTo.Focus()
            labelPositionTo.Visible = *True 
            labelPositionTo.Text = "Position-to failed for: " + Customer_CMName
        EndIf
        
        textboxPositionTo.Text = String.Empty		
	EndSr

    BegSr CheckForStartingPosition
        If ( Session[ "number" ] <> *Nothing ) AND +     
           ( Session[ "name"   ] <> *Nothing ) 
            Customer_CMCustNo = Session[ "number" ].ToString() 
            Customer_CMName   = Session[ "name" ].ToString() 
               
            SetLL CustomerByName Key( Customer_CMName, Customer_CMCustNo ) 
               
            Session.Remove( "number" )
            Session.Remove( "name"  )
        EndIf             
    EndSr                        

EndClass
